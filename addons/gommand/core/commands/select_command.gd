class_name SelectCommand
extends Command

# Choose a command based on a selector callable returning a key, and a dictionary mapping keys to commands.
const FunctionalTools = preload("../../scripts/functional_tools.gd")
var _selector_callable: Callable = Callable()
var _command_map: Dictionary = {}
var _active_command: Command = null


func _init(selector_callable: Callable, command_map: Dictionary) -> void:
	_selector_callable = selector_callable
	_command_map = command_map.duplicate()
	# Aggregate requirements conservatively (union of all map commands)
	super._init([], true)
	FunctionalTools.for_each(
		_command_map.keys(),
		func(mapped_key):
			var mapped_command: Command = _command_map[mapped_key]
			if mapped_command == null:
				return
			FunctionalTools.for_each(
				mapped_command.get_requirements(), func(subsystem): add_requirement(subsystem)
			)
	)


func initialize() -> void:
	var selected_key = _selector_callable.call() if _selector_callable.is_valid() else null
	_active_command = _command_map.get(selected_key)
	if _active_command != null and not _active_command._has_initialized():
		_active_command.initialize()
		_active_command._mark_initialized()


func execute(delta_time: float) -> void:
	if _active_command != null:
		_active_command.execute(delta_time)


func physics_execute(delta_time: float) -> void:
	if _active_command != null:
		_active_command.physics_execute(delta_time)


func is_finished() -> bool:
	return _active_command == null or _active_command.is_finished()


func end(interrupted: bool) -> void:
	if _active_command != null:
		_active_command.end(interrupted)
